/*
TODO :
- Alternate liveries
- 2CC style purchase
*/

template tmpl_vehicle_131TB_8_views(frame) {
     //xpos ypos xsize ysize xrel yrel
    [5, 5 + frame * 29, 8, 24, -3, -16]
    [18, 5 + frame * 29, 22, 17, -12, -11]
    [45, 5 + frame * 29, 32, 14, -12, -9]
    [82, 5 + frame * 29, 22, 17, -5, -10]
    [109, 5 + frame * 29, 8, 24,  -3, -15]
    [122, 5 + frame * 29, 22, 17, -14, -10]
    [149, 5 + frame * 29, 32, 14, -24, -10]
    [186, 5 + frame * 29, 22, 17, -8, -11]
}

template tmpl_vehicle_131TB_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [149, 5, 32, 14, -25, -9]
}

spriteset (set_131TB_graphics_0,        "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(0) }
spriteset (set_131TB_graphics_1,        "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(1) }
spriteset (set_131TB_graphics_2,        "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(2) }
spriteset (set_131TB_graphics_3,        "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(3) }
spriteset (set_131TB_graphics_purchase, "gfx/131TB.png") { tmpl_vehicle_131TB_purchase() }

switch (FEAT_TRAINS, SELF, sw_131TB_graphics, motion_counter % 4) {
    0: set_131TB_graphics_0;
    1: set_131TB_graphics_1;
    2: set_131TB_graphics_2;
    3: set_131TB_graphics_3;
}

item(FEAT_TRAINS, item_131TB) {
    property {
        name:                         string(STR_SNCF_TRAIN_131TB_NAME);
        climates_available:           bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL); 
        introduction_date:            date(1924, 1, 1);
        model_life:                   55;
        vehicle_life:                 22;
        reliability_decay:            6;
        refittable_cargo_classes:     bitmask();
        non_refittable_cargo_classes: bitmask();
        loading_speed:                0; 
        cost_factor:                  14;
        running_cost_factor:          100;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        90 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER | AI_FLAG_CARGO;
        power:                        1000 hp;
        running_cost_base:            RUNNING_COST_STEAM;
        dual_headed:                  0;
        cargo_capacity:               0;
        weight:                       77.4 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_STEAM;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.155;
        air_drag_coefficient:         0.06;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_STEAM, 2, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
        length:                       7;
    }
    graphics {
        default:                      sw_131TB_graphics;
        purchase:                     set_131TB_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_131TB_ADDITIONAL_TEXT);
    }
}
