template tmpl_vehicle_131TB_8_views(frame, livery) {
     //xpos ypos xsize ysize xrel yrel
    [5 + livery * 208, 5 + frame * 29, 8, 24, -3, -16]
    [18 + livery * 208, 5 + frame * 29, 22, 17, -9, -12]
    [45 + livery * 208, 5 + frame * 29, 32, 14, -7, -10]
    [82 + livery * 208, 5 + frame * 29, 22, 17, -5, -8]
    [109 + livery * 208, 5 + frame * 29, 8, 24,  -3, -15]
    [122 + livery * 208, 5 + frame * 29, 22, 17, -19, -6]
    [149 + livery * 208, 5 + frame * 29, 32, 14, -28, -10]
    [186 + livery * 208, 5 + frame * 29, 22, 17, -12, -12]
}

template tmpl_vehicle_131TB_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [149, 5, 32, 14, -29, -9]
}

spriteset (set_131TB_graphics_0_0,      "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(0, 0) }
spriteset (set_131TB_graphics_1_0,      "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(1, 0) }
spriteset (set_131TB_graphics_2_0,      "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(2, 0) }
spriteset (set_131TB_graphics_3_0,      "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(3, 0) }
spriteset (set_131TB_graphics_0_1,      "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(0, 1) }
spriteset (set_131TB_graphics_1_1,      "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(1, 1) }
spriteset (set_131TB_graphics_2_1,      "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(2, 1) }
spriteset (set_131TB_graphics_3_1,      "gfx/131TB.png") { tmpl_vehicle_131TB_8_views(3, 1) }
spriteset (set_131TB_graphics_purchase, "gfx/131TB.png") { tmpl_vehicle_131TB_purchase() }

switch (FEAT_TRAINS, SELF, sw_131TB_graphics_0, motion_counter % 4) {
    0: set_131TB_graphics_0_0;
    1: set_131TB_graphics_1_0;
    2: set_131TB_graphics_2_0;
    3: set_131TB_graphics_3_0;
}

switch (FEAT_TRAINS, SELF, sw_131TB_graphics_1, motion_counter % 4) {
    0: set_131TB_graphics_0_1;
    1: set_131TB_graphics_1_1;
    2: set_131TB_graphics_2_1;
    3: set_131TB_graphics_3_1;
}

random_switch (FEAT_TRAINS, SELF, sw_131TB_graphics_random) {
    1: sw_131TB_graphics_0;
    2: sw_131TB_graphics_1;
}

item(FEAT_TRAINS, item_131TB) {
    property {
        name:                         string(STR_SNCF_TRAIN_131TB_NAME);
        climates_available:           bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL); 
        introduction_date:            date(1924, 1, 1);
        model_life:                   55;
        vehicle_life:                 22;
        reliability_decay:            6;
        refittable_cargo_classes:     bitmask();
        non_refittable_cargo_classes: bitmask();
        loading_speed:                0; 
        cost_factor:                  14;
        running_cost_factor:          100;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        90 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER | AI_FLAG_CARGO;
        power:                        1000 hp;
        running_cost_base:            RUNNING_COST_STEAM;
        dual_headed:                  0;
        cargo_capacity:               0;
        weight:                       77.4 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_STEAM;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.155;
        air_drag_coefficient:         0.06;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
        length:                       5;
    }
    graphics {
        default:                      sw_131TB_graphics_random;
        purchase:                     set_131TB_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_131TB_ADDITIONAL_TEXT);
    }
}
