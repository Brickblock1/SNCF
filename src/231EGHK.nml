/*
TODO :
- alternative liveries
- 2CC style purchase
- fix smoke
- Express only
*/

template tmpl_vehicle_231EGHK_8_views(frame) {
    //xpos ypos xsize ysize xrel yrel
    [2, 4 + frame * 26, 8, 24, -3, -16]
    [12, 4 + frame * 26, 22, 17, -10, -10]
    [36, 4 + frame * 26, 32, 14, -12, -9]
    [70, 4 + frame * 26, 22, 17, -2, -7]
    [94, 4 + frame * 26, 8, 24, -3, -12]
    [104, 4 + frame * 26, 22, 17, -19, -6]
    [128, 4 + frame * 26, 32, 14, -22, -9]
    [162, 4 + frame * 26, 22, 17, -9, -10]
}

template tmpl_vehicle_231EGHK_TENDER_8_views() {
    //xpos ypos xsize ysize xrel yrel
    [2, 160, 8, 24, -3, -16]
    [12, 160, 22, 17, -4, -14]
    [36, 160, 32, 14, 3, -9]
    [70, 160, 22, 17, -3, -8]
    [94, 160, 8, 24,  -3, -12]
    [104, 160, 22, 17, -19, -7]
    [128, 160, 32, 14, -37, -10]
    [162, 160, 22, 17, -17, -15]
}

template tmpl_vehicle_231EGHK_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [128, 4, 32, 14, -24, -10]
}

spriteset (set_231EGHK_graphics_0,        "gfx/231EGHK.png") { tmpl_vehicle_231EGHK_8_views(0) }
spriteset (set_231EGHK_graphics_1,        "gfx/231EGHK.png") { tmpl_vehicle_231EGHK_8_views(1) }
spriteset (set_231EGHK_graphics_2,        "gfx/231EGHK.png") { tmpl_vehicle_231EGHK_8_views(2) }
spriteset (set_231EGHK_graphics_3,        "gfx/231EGHK.png") { tmpl_vehicle_231EGHK_8_views(3) }
spriteset (set_231EGHK_graphics_4,        "gfx/231EGHK.png") { tmpl_vehicle_231EGHK_8_views(4) }
spriteset (set_231EGHK_graphics_5,        "gfx/231EGHK.png") { tmpl_vehicle_231EGHK_8_views(5) }
spriteset (set_231EGHK_tender_graphics,   "gfx/231EGHK.png") { tmpl_vehicle_231EGHK_TENDER_8_views() }
spriteset (set_231EGHK_graphics_purchase, "gfx/231EGHK.png") { tmpl_vehicle_231EGHK_purchase() }

switch (FEAT_TRAINS, SELF, sw_231EGHK_graphics_animated, motion_counter % 6) {
    0: set_231EGHK_graphics_0;
    1: set_231EGHK_graphics_1;
    2: set_231EGHK_graphics_2;
    3: set_231EGHK_graphics_3;
    4: set_231EGHK_graphics_4;
    5: set_231EGHK_graphics_5;
}

switch(FEAT_TRAINS, SELF, sw_231EGHK_graphics, position_in_articulated_veh) {
    0: sw_231EGHK_graphics_animated;
    1: set_231EGHK_tender_graphics;
}

switch(FEAT_TRAINS, SELF, sw_231EGHK_articulated_part, extra_callback_info1) {
    1 : return item_231EGHK;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_TRAINS, item_231EGHK) {
    property {
        name:                         string(STR_SNCF_TRAIN_231EGHK_NAME);
        climates_available:           bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL); 
        introduction_date:            date(1934, 1, 1);
        model_life:                   37;
        vehicle_life:                 25;
        reliability_decay:            11;
        refittable_cargo_classes:     bitmask();
        non_refittable_cargo_classes: bitmask();
        loading_speed:                0; 
        cost_factor:                  37;
        running_cost_factor:          165;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        130 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER | AI_FLAG_CARGO;
        power:                        3000 hp;
        running_cost_base:            RUNNING_COST_STEAM;
        dual_headed:                  0;
        cargo_capacity:               0;
        weight:                       180 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_STEAM;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.140;
        air_drag_coefficient:         0.06;
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
    }
    graphics {
        default:                      sw_231EGHK_graphics;
        purchase:                     set_231EGHK_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_231EGHK_ADDITIONAL_TEXT);
        articulated_part:             sw_231EGHK_articulated_part;
        visual_effect_and_powered:    return (position_in_articulated_veh == 0) ?
                                          visual_effect_and_powered(VISUAL_EFFECT_STEAM, 7, DISABLE_WAGON_POWER) :
                                          visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        length:                       return (position_in_articulated_veh == 0) ? 7 : 3;
        can_attach_wagon:             sw_attach_only_express_mail_passenger;
    }
}
