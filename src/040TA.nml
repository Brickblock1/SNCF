/*
TODO :
- Alternate liveries
- No express
- 2CC style purchase
*/

template tmpl_vehicle_040TA_8_views(frame) {
     //xpos ypos xsize ysize xrel yrel
    [5, 5 + frame * 29, 8, 24, -3, -11]
    [18, 5 + frame * 29, 22, 17, -12, -11]
    [45, 5 + frame * 29, 32, 14, -17, -9]
    [82, 5 + frame * 29, 22, 17, -4, -9]
    [109, 5 + frame * 29, 8, 24,  -3, -15]
    [122, 5 + frame * 29, 22, 17, -14, -11]
    [149, 5 + frame * 29, 32, 14, -17, -10]
    [186, 5 + frame * 29, 22, 17, -6, -10]
}

template tmpl_vehicle_040TA_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [149, 5, 32, 14, -13, -9]
}

spriteset (set_040TA_graphics_0,        "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(0) }
spriteset (set_040TA_graphics_1,        "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(1) }
spriteset (set_040TA_graphics_2,        "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(2) }
spriteset (set_040TA_graphics_3,        "gfx/040TA.png") { tmpl_vehicle_040TA_8_views(3) }
spriteset (set_040TA_graphics_purchase, "gfx/040TA.png") { tmpl_vehicle_040TA_purchase() }

switch (FEAT_TRAINS, SELF, sw_040TA_graphics, motion_counter % 4) {
    0: set_040TA_graphics_0;
    1: set_040TA_graphics_1;
    2: set_040TA_graphics_2;
    3: set_040TA_graphics_3;
}

item(FEAT_TRAINS, item_040TA) {
    property {
        name:                         string(STR_SNCF_TRAIN_040TA_NAME);
        climates_available:           bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL); 
        introduction_date:            date(1914, 1, 1);
        model_life:                   48;
        vehicle_life:                 18;
        reliability_decay:            9;
        refittable_cargo_classes:     bitmask();
        non_refittable_cargo_classes: bitmask();
        loading_speed:                0; 
        cost_factor:                  7;
        running_cost_factor:          80;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        45 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER | AI_FLAG_CARGO;
        power:                        500 hp;
        running_cost_base:            RUNNING_COST_STEAM;
        dual_headed:                  0;
        cargo_capacity:               0;
        weight:                       60 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_STEAM;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.127;
        air_drag_coefficient:         0.06;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_STEAM, 2, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
        length:                       5;
    }
    graphics {
        default:                      sw_040TA_graphics;
        purchase:                     set_040TA_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_040TA_ADDITIONAL_TEXT);
    }
}
