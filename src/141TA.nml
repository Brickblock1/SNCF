template tmpl_vehicle_141TA_8_views(frame, livery) {
    //xpos ypos xsize ysize xrel yrel
    [5 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 8, 24, -3, -16]
    [18 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 22, 17, -12, -11]
    [45 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 32, 14, -8, -9]
    [82 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 22, 17, -5, -8]
    [109 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 8, 24,  -3, -15]
    [122 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 22, 17, -17, -7]
    [149 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 32, 14, -27, -9]
    [186 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 116, 22, 17, -10, -11]
}

template tmpl_vehicle_141TA_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [149, 5, 32, 14, -29, -9]
}

spriteset (set_141TA_graphics_0_0,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(0, 0) }
spriteset (set_141TA_graphics_1_0,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(1, 0) }
spriteset (set_141TA_graphics_2_0,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(2, 0) }
spriteset (set_141TA_graphics_3_0,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(3, 0) }
spriteset (set_141TA_graphics_0_1,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(0, 1) }
spriteset (set_141TA_graphics_1_1,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(1, 1) }
spriteset (set_141TA_graphics_2_1,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(2, 1) }
spriteset (set_141TA_graphics_3_1,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(3, 1) }
spriteset (set_141TA_graphics_0_2,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(0, 2) }
spriteset (set_141TA_graphics_1_2,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(1, 2) }
spriteset (set_141TA_graphics_2_2,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(2, 2) }
spriteset (set_141TA_graphics_3_2,      "gfx/141TA.png") { tmpl_vehicle_141TA_8_views(3, 2) }
spriteset (set_141TA_graphics_purchase, "gfx/141TA.png") { tmpl_vehicle_141TA_purchase() }

switch (FEAT_TRAINS, SELF, sw_141TA_graphics_0, motion_counter % 4) {
    0: set_141TA_graphics_0_0;
    1: set_141TA_graphics_1_0;
    2: set_141TA_graphics_2_0;
    3: set_141TA_graphics_3_0;
}

switch (FEAT_TRAINS, SELF, sw_141TA_graphics_1, motion_counter % 4) {
    0: set_141TA_graphics_0_1;
    1: set_141TA_graphics_1_1;
    2: set_141TA_graphics_2_1;
    3: set_141TA_graphics_3_1;
}

switch (FEAT_TRAINS, SELF, sw_141TA_graphics_2, motion_counter % 4) {
    0: set_141TA_graphics_0_2;
    1: set_141TA_graphics_1_2;
    2: set_141TA_graphics_2_2;
    3: set_141TA_graphics_3_2;
}

random_switch (FEAT_TRAINS, SELF, sw_141TA_graphics_random) {
    1: sw_141TA_graphics_0;
    2: sw_141TA_graphics_1;
    3: sw_141TA_graphics_2;
}

item(FEAT_TRAINS, item_141TA) {
    property {
        name:                         string(STR_SNCF_TRAIN_141TA_NAME);
        climates_available:           bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL); 
        introduction_date:            date(1922, 1, 1);
        model_life:                   46;
        vehicle_life:                 23;
        reliability_decay:            8;
        refittable_cargo_classes:     NO_CARGO_CLASS;
        non_refittable_cargo_classes: ALL_CARGO_CLASSES;
        loading_speed:                0; 
        cost_factor:                  15;
        running_cost_factor:          90;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        70 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER | AI_FLAG_CARGO;
        power:                        1360 hp;
        running_cost_base:            RUNNING_COST_STEAM;
        dual_headed:                  0;
        cargo_capacity:               0;
        weight:                       94 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_STEAM;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.286;
        air_drag_coefficient:         0.06;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_STEAM, -4, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
        length:                       6;
    }
    graphics {
        default:                      sw_141TA_graphics_random;
        purchase:                     set_141TA_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_141TA_ADDITIONAL_TEXT);
    }
}
