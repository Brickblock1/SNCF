template tmpl_vehicle_231CHAPELON_8_views_front(frame) {
    //xpos ypos xsize ysize xrel yrel
    [213, 5 + frame * 29, 8, 24, -3, -25]
    [226, 5 + frame * 29, 22, 17, -10, -11]
    [253, 5 + frame * 29, 32, 14, -15, -9]
    [290, 5 + frame * 29, 22, 17, 1, -5]
    [317, 5 + frame * 29, 8, 24, -3, -7]
    [330, 5 + frame * 29, 22, 17, -20, -5]
    [357, 5 + frame * 29, 32, 14, -26, -10]
    [394, 5 + frame * 29, 22, 17, -10, -12]
}

template tmpl_vehicle_231CHAPELON_8_views_middle() {
    //xpos ypos xsize ysize xrel yrel
    [5, 179,  8, 24, -3, -21]
    [18, 179, 22, 17, -11, -14]
    [45, 179, 32, 14, -9, -10]
    [82, 179, 22, 17, 1, -8]
    [109, 179,  8, 24, -3, -8]
    [122, 179, 22, 17, -21, -8]
    [149, 179, 32, 14, -22, -11]
    [186, 179, 22, 17, -9, -14]
}

template tmpl_vehicle_231CHAPELON_8_views_rear(frame) {
    //xpos ypos xsize ysize xrel yrel
    [109, 5 + frame * 29, 8, 24, -3, -21]
    [122, 5 + frame * 29, 22, 17, -10, -11]
    [149, 5 + frame * 29, 32, 14, -9, -9]
    [186, 5 + frame * 29, 22, 17, 3, -5]
    [5, 5 + frame * 29, 8, 24, -3, -13]
    [18, 5 + frame * 29, 22, 17, -22, -5]
    [45, 5 + frame * 29, 32, 14, -32, -10]
    [82, 5 + frame * 29, 22, 17, -10, -12]
}

template tmpl_vehicle_231CHAPELON_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [0, 0, 54, 14, -43, -10]
}

spriteset(set_231CHAPELON_graphics_front_0, "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_front(0) }
spriteset(set_231CHAPELON_graphics_front_1, "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_front(1) }
spriteset(set_231CHAPELON_graphics_front_2, "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_front(2) }
spriteset(set_231CHAPELON_graphics_front_3, "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_front(3) }
spriteset(set_231CHAPELON_graphics_front_4, "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_front(4) }
spriteset(set_231CHAPELON_graphics_front_5, "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_front(5) }
spriteset(set_231CHAPELON_graphics_middle,  "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_middle() }
spriteset(set_231CHAPELON_graphics_rear_0,  "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_rear(0) }
spriteset(set_231CHAPELON_graphics_rear_1,  "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_rear(1) }
spriteset(set_231CHAPELON_graphics_rear_2,  "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_rear(2) }
spriteset(set_231CHAPELON_graphics_rear_3,  "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_rear(3) }
spriteset(set_231CHAPELON_graphics_rear_4,  "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_rear(4) }
spriteset(set_231CHAPELON_graphics_rear_5,  "gfx/231CHAPELON.png") { tmpl_vehicle_231CHAPELON_8_views_rear(5) }
spriteset(set_231CHAPELON_graphics_purchase,  "gfx/231CHAPELON_purchase.png") { tmpl_vehicle_231CHAPELON_purchase() }

switch(FEAT_TRAINS, SELF, sw_231CHAPELON_graphics_front, motion_counter % 6) {
    0: set_231CHAPELON_graphics_front_0;
    1: set_231CHAPELON_graphics_front_1;
    2: set_231CHAPELON_graphics_front_2;
    3: set_231CHAPELON_graphics_front_3;
    4: set_231CHAPELON_graphics_front_4;
    5: set_231CHAPELON_graphics_front_5;
}

switch(FEAT_TRAINS, SELF, sw_231CHAPELON_graphics_rear, motion_counter % 6) {
    0: set_231CHAPELON_graphics_rear_0;
    1: set_231CHAPELON_graphics_rear_1;
    2: set_231CHAPELON_graphics_rear_2;
    3: set_231CHAPELON_graphics_rear_3;
    4: set_231CHAPELON_graphics_rear_4;
    5: set_231CHAPELON_graphics_rear_5;
}

switch(FEAT_TRAINS, SELF, sw_231CHAPELON_graphics, position_in_articulated_veh) {
    0: sw_231CHAPELON_graphics_front;
    1: set_231CHAPELON_graphics_middle;
    2: sw_231CHAPELON_graphics_rear;
}

switch(FEAT_TRAINS, SELF, sw_231CHAPELON_length, position_in_articulated_veh) {
    0: 4;
    1: 5;
    2: 4;
}

switch(FEAT_TRAINS, SELF, sw_231CHAPELON_articulated_part, extra_callback_info1) {
    1 .. 2 : return item_231CHAPELON;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_TRAINS, item_231CHAPELON) {
    property {
        name:                         string(STR_SNCF_TRAIN_231CHAPELON_NAME);
        climates_available:           bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL); 
        introduction_date:            date(1934, 1, 1);
        model_life:                   37;
        vehicle_life:                 25;
        reliability_decay:            11;
        refittable_cargo_classes:     NO_CARGO_CLASS;
        non_refittable_cargo_classes: ALL_CARGO_CLASSES;
        loading_speed:                0; 
        cost_factor:                  37;
        running_cost_factor:          165;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        140 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER;
        power:                        3000 hp;
        running_cost_base:            RUNNING_COST_STEAM;
        dual_headed:                  0;
        cargo_capacity:               48;
        weight:                       180 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_STEAM;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.3;
        air_drag_coefficient:         0.06;
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
        length:                       4;
    }
    graphics {
        default:                      sw_231CHAPELON_graphics;
        purchase:                     set_231CHAPELON_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_231CHAPELON_ADDITIONAL_TEXT);
        articulated_part:             sw_231CHAPELON_articulated_part;
        visual_effect_and_powered:    return (position_in_articulated_veh == 0) ?
                                          visual_effect_and_powered(VISUAL_EFFECT_STEAM, -3, DISABLE_WAGON_POWER) :
                                          visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        length:                       sw_231CHAPELON_length;
    }
}
