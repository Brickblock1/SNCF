/*
TODO :
- alternative liveries
- 2CC style purchase
- fix smoke
*/

template tmpl_vehicle_230BDG_8_views(frame) {
    //xpos ypos xsize ysize xrel yrel
    [5, 5 + frame * 29, 8, 24, -3, -16]
    [18, 5 + frame * 29, 22, 17, -10, -12]
    [45, 5 + frame * 29, 32, 14, -7, -9]
    [82, 5 + frame * 29, 22, 17, -3, -8]
    [109, 5 + frame * 29, 8, 24,  -3, -15]
    [122, 5 + frame * 29, 22, 17, -17, -8]
    [149, 5 + frame * 29, 32, 14, -26, -10]
    [185, 5 + frame * 29, 22, 17, -8, -11]
}

template tmpl_vehicle_230BDG_TENDER_8_views() {
    //xpos ypos xsize ysize xrel yrel
    [5, 121, 8, 24, -3, -16]
    [18, 121, 22, 17, -4, -14]
    [45, 121, 32, 14, -16, -9]
    [82, 121, 22, 17, -4, -9]
    [109, 121, 8, 24,  -3, -15]
    [122, 121, 22, 17, -17, -9]
    [149, 121, 32, 14, -37, -10]
    [185, 121, 22, 17, -8, -11]
}

template tmpl_vehicle_230BDG_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [149, 5, 32, 14, -24, -10]
}

spriteset (set_230BDG_graphics_0,        "gfx/230BDG.png") { tmpl_vehicle_230BDG_8_views(0) }
spriteset (set_230BDG_graphics_1,        "gfx/230BDG.png") { tmpl_vehicle_230BDG_8_views(1) }
spriteset (set_230BDG_graphics_2,        "gfx/230BDG.png") { tmpl_vehicle_230BDG_8_views(2) }
spriteset (set_230BDG_graphics_3,        "gfx/230BDG.png") { tmpl_vehicle_230BDG_8_views(3) }
spriteset (set_230BDG_tender_graphics,   "gfx/230BDG.png") { tmpl_vehicle_230BDG_TENDER_8_views() }
spriteset (set_230BDG_graphics_purchase, "gfx/230BDG.png") { tmpl_vehicle_230BDG_purchase() }

switch (FEAT_TRAINS, SELF, sw_230BDG_graphics_animated, motion_counter % 4) {
    0: set_230BDG_graphics_0;
    1: set_230BDG_graphics_1;
    2: set_230BDG_graphics_2;
    3: set_230BDG_graphics_3;
}

switch(FEAT_TRAINS, SELF, sw_230BDG_graphics, position_in_articulated_veh) {
    0:      sw_230BDG_graphics_animated;
    1:      set_230BDG_tender_graphics;
}

switch(FEAT_TRAINS, SELF, sw_230BDG_articulated_part, extra_callback_info1) {
    1 : return item_230BDG;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_TRAINS, item_230BDG) {
    property {
        name:                         string(STR_SNCF_TRAIN_230BDG_NAME);
        climates_available:           bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL); 
        introduction_date:            date(1914, 1, 1);
        model_life:                   41;
        vehicle_life:                 25;
        reliability_decay:            11;
        refittable_cargo_classes:     bitmask();
        non_refittable_cargo_classes: bitmask();
        loading_speed:                0; 
        cost_factor:                  23;
        running_cost_factor:          150;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        110 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER | AI_FLAG_CARGO;
        power:                        1600 hp;
        running_cost_base:            RUNNING_COST_STEAM;
        dual_headed:                  0;
        cargo_capacity:               0;
        weight:                       184 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_STEAM;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.316;
        air_drag_coefficient:         0.06;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_STEAM, 2, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
    }
    graphics {
        default:                      sw_230BDG_graphics;
        purchase:                     set_230BDG_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_230BDG_ADDITIONAL_TEXT);
        articulated_part:             sw_230BDG_articulated_part;
        visual_effect_and_powered:    return (position_in_articulated_veh == 0) ?
                                          visual_effect_and_powered(VISUAL_EFFECT_STEAM, 2, DISABLE_WAGON_POWER) :
                                          visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        length:                       return (position_in_articulated_veh == 0) ? 6 : 3;
    }
}
