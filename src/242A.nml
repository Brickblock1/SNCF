template tmpl_vehicle_242A_8_views(frame) {
    //xpos ypos xsize ysize xrel yrel
    [5, 5 + frame * 29, 8, 24, -3, -14]
    [18, 5 + frame * 29, 22, 17, -14, -9]
    [45, 5 + frame * 29, 32, 14, -17, -10]
    [82, 5 + frame * 29, 22, 17, -5, -9]
    [109, 5 + frame * 29, 8, 24, -3, -13]
    [122, 5 + frame * 29, 22, 17, -18, -7]
    [149, 5 + frame * 29, 32, 14, -18, -10]
    [186, 5 + frame * 29, 22, 17, -11, -10]
}

template tmpl_vehicle_242A_TENDER_8_views() {
    //xpos ypos xsize ysize xrel yrel
    [5, 179, 8, 24, -3, -19]
    [18, 179, 22, 17, -8, -12]
    [45, 179, 32, 14, -1, -10]
    [82, 179, 22, 17, -4, -9]
    [109, 179, 8, 24, -3, -11]
    [122, 179, 22, 17, -19, -7]
    [149, 179, 32, 14, -34, -10]
    [186, 179, 22, 17, -17, -15]
}

template tmpl_vehicle_242A_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [149, 5, 32, 14, -29, -10]
}

spriteset (set_242A_graphics_0,        "gfx/242A.png") { tmpl_vehicle_242A_8_views(0) }
spriteset (set_242A_graphics_1,        "gfx/242A.png") { tmpl_vehicle_242A_8_views(1) }
spriteset (set_242A_graphics_2,        "gfx/242A.png") { tmpl_vehicle_242A_8_views(2) }
spriteset (set_242A_graphics_3,        "gfx/242A.png") { tmpl_vehicle_242A_8_views(3) }
spriteset (set_242A_graphics_4,        "gfx/242A.png") { tmpl_vehicle_242A_8_views(4) }
spriteset (set_242A_graphics_5,        "gfx/242A.png") { tmpl_vehicle_242A_8_views(5) }
spriteset (set_242A_tender_graphics,   "gfx/242A.png") { tmpl_vehicle_242A_TENDER_8_views() }
spriteset (set_242A_graphics_purchase, "gfx/242A.png") { tmpl_vehicle_242A_purchase() }

switch (FEAT_TRAINS, SELF, sw_242A_graphics_animated, motion_counter % 6) {
    0: set_242A_graphics_0;
    1: set_242A_graphics_1;
    2: set_242A_graphics_2;
    3: set_242A_graphics_3;
    4: set_242A_graphics_4;
    5: set_242A_graphics_5;
}

switch(FEAT_TRAINS, SELF, sw_242A_graphics, position_in_articulated_veh) {
    0: sw_242A_graphics_animated;
    1: set_242A_tender_graphics;
}

switch(FEAT_TRAINS, SELF, sw_242A_articulated_part, extra_callback_info1) {
    1 : return item_242A;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_TRAINS, item_242A) {
    property {
        name:                         string(STR_SNCF_TRAIN_242A_NAME);
        climates_available:           bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL); 
        introduction_date:            date(1946, 1, 1);
        model_life:                   39;
        vehicle_life:                 29;
        reliability_decay:            12;
        refittable_cargo_classes:     NO_CARGO_CLASS;
        non_refittable_cargo_classes: ALL_CARGO_CLASSES;
        loading_speed:                0; 
        cost_factor:                  52;
        running_cost_factor:          175;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        150 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER | AI_FLAG_CARGO;
        power:                        5300 hp;
        running_cost_base:            RUNNING_COST_STEAM;
        dual_headed:                  0;
        cargo_capacity:               0;
        weight:                       198 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_STEAM;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.3;
        air_drag_coefficient:         0.06;
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
    }
    graphics {
        default:                      sw_242A_graphics;
        purchase:                     set_242A_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_242A_ADDITIONAL_TEXT);
        articulated_part:             sw_242A_articulated_part;
        visual_effect_and_powered:    return (position_in_articulated_veh == 0) ?
                                          visual_effect_and_powered(VISUAL_EFFECT_STEAM, -3, DISABLE_WAGON_POWER) :
                                          visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        length:                       return (position_in_articulated_veh == 0) ? 8 : 4;
    }
}
