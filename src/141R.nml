/*
TODO :
- 2CC style purchase
*/

template tmpl_vehicle_141R_8_views(frame, livery) {
    //xpos ypos xsize ysize xrel yrel
    [5 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 145, 8, 24, -3, -16]
    [18 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 145, 22, 17, -12, -10]
    [45 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 145, 32, 14, -12, -9]
    [82 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 145, 22, 17, -5, -9]
    [109 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 145, 8, 24,  -3, -15]
    [122 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 145, 22, 17, -16, -8]
    [149 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 145, 32, 14, -23, -10]
    [186 + livery % 2 * 208, 5 + frame * 29 + int(livery / 2) * 145, 22, 17, -8, -9]
}

template tmpl_vehicle_141R_TENDER_8_views(livery) {
    //xpos ypos xsize ysize xrel yrel
    [5 + livery % 2 * 208, 121 + int(livery / 2) * 145, 8, 24, -3, -16]
    [18 + livery % 2 * 208, 121 + int(livery / 2) * 145, 22, 17, -8, -12]
    [45 + livery % 2 * 208, 121 + int(livery / 2) * 145, 32, 14, 0, -9]
    [82 + livery % 2 * 208, 121 + int(livery / 2) * 145, 22, 17, -5, -9]
    [109 + livery % 2 * 208, 121 + int(livery / 2) * 145, 8, 24,  -3, -15]
    [122 + livery % 2 * 208, 121 + int(livery / 2) * 145, 22, 17, -17, -7]
    [149 + livery % 2 * 208, 121 + int(livery / 2) * 145, 32, 14, -35, -10]
    [186 + livery % 2 * 208, 121 + int(livery / 2) * 145, 22, 17, -15, -13]
}

template tmpl_vehicle_141R_purchase() {
    //xpos ypos xsize ysize xrel yrel
    [149, 5, 32, 14, -28, -10]
}

spriteset (set_141R_graphics_0_0,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(0, 0) }
spriteset (set_141R_graphics_1_0,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(1, 0) }
spriteset (set_141R_graphics_2_0,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(2, 0) }
spriteset (set_141R_graphics_3_0,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(3, 0) }
spriteset (set_141R_graphics_0_1,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(0, 1) }
spriteset (set_141R_graphics_1_1,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(1, 1) }
spriteset (set_141R_graphics_2_1,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(2, 1) }
spriteset (set_141R_graphics_3_1,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(3, 1) }
spriteset (set_141R_graphics_0_2,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(0, 2) }
spriteset (set_141R_graphics_1_2,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(1, 2) }
spriteset (set_141R_graphics_2_2,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(2, 2) }
spriteset (set_141R_graphics_3_2,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(3, 2) }
spriteset (set_141R_graphics_0_3,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(0, 3) }
spriteset (set_141R_graphics_1_3,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(1, 3) }
spriteset (set_141R_graphics_2_3,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(2, 3) }
spriteset (set_141R_graphics_3_3,      "gfx/141R.png") { tmpl_vehicle_141R_8_views(3, 3) }
spriteset (set_141R_tender_graphics_0, "gfx/141R.png") { tmpl_vehicle_141R_TENDER_8_views(0) }
spriteset (set_141R_tender_graphics_1, "gfx/141R.png") { tmpl_vehicle_141R_TENDER_8_views(1) }
spriteset (set_141R_tender_graphics_2, "gfx/141R.png") { tmpl_vehicle_141R_TENDER_8_views(2) }
spriteset (set_141R_tender_graphics_3, "gfx/141R.png") { tmpl_vehicle_141R_TENDER_8_views(3) }
spriteset (set_141R_graphics_purchase, "gfx/141R.png") { tmpl_vehicle_141R_purchase() }

switch (FEAT_TRAINS, SELF, sw_141R_graphics_0, motion_counter % 4) {
    0: set_141R_graphics_0_0;
    1: set_141R_graphics_1_0;
    2: set_141R_graphics_2_0;
    3: set_141R_graphics_3_0;
}

switch (FEAT_TRAINS, SELF, sw_141R_graphics_1, motion_counter % 4) {
    0: set_141R_graphics_0_1;
    1: set_141R_graphics_1_1;
    2: set_141R_graphics_2_1;
    3: set_141R_graphics_3_1;
}

switch (FEAT_TRAINS, SELF, sw_141R_graphics_2, motion_counter % 4) {
    0: set_141R_graphics_0_2;
    1: set_141R_graphics_1_2;
    2: set_141R_graphics_2_2;
    3: set_141R_graphics_3_2;
}

switch (FEAT_TRAINS, SELF, sw_141R_graphics_3, motion_counter % 4) {
    0: set_141R_graphics_0_3;
    1: set_141R_graphics_1_3;
    2: set_141R_graphics_2_3;
    3: set_141R_graphics_3_3;
}

random_switch (FEAT_TRAINS, SELF, sw_141R_graphics_random) {
    1: sw_141R_graphics_0;
    2: sw_141R_graphics_1;
    3: sw_141R_graphics_2;
    4: sw_141R_graphics_3;
}

random_switch (FEAT_TRAINS, SELF, sw_141R_graphics_tender_random) {
    1: set_141R_tender_graphics_0;
    2: set_141R_tender_graphics_1;
    3: set_141R_tender_graphics_2;
    4: set_141R_tender_graphics_3;
}

switch(FEAT_TRAINS, SELF, sw_141R_graphics, position_in_articulated_veh) {
    0:      sw_141R_graphics_random;
    1:      sw_141R_graphics_tender_random;
}

switch(FEAT_TRAINS, SELF, sw_141R_articulated_part, extra_callback_info1) {
    1 : return item_141R;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

item(FEAT_TRAINS, item_141R) {
    property {
        name:                         string(STR_SNCF_TRAIN_141R_NAME);
        climates_available:           bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL); 
        introduction_date:            date(1945, 1, 1);
        model_life:                   44;
        vehicle_life:                 33;
        reliability_decay:            14;
        refittable_cargo_classes:     bitmask();
        non_refittable_cargo_classes: bitmask();
        loading_speed:                0; 
        cost_factor:                  24;
        running_cost_factor:          120;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        speed:                        105 km/h;
        misc_flags:                   bitmask();
        refit_cost:                   0;
        track_type:                   UNELECTRIFIED;
        ai_special_flag:              AI_FLAG_PASSENGER | AI_FLAG_CARGO;
        power:                        2928 hp;
        running_cost_base:            RUNNING_COST_STEAM;
        dual_headed:                  0;
        cargo_capacity:               0;
        weight:                       176 ton;
        ai_engine_rank:               0;
        engine_class:                 ENGINE_CLASS_STEAM;
        extra_power_per_wagon:        0 kW;
        tractive_effort_coefficient:  0.228;
        air_drag_coefficient:         0.06;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_STEAM, 2, DISABLE_WAGON_POWER);
        extra_weight_per_wagon:       0 ton;
        bitmask_vehicle_info:         0;
    }
    graphics {
        default:                      sw_141R_graphics;
        purchase:                     set_141R_graphics_purchase;
        additional_text:              return string(STR_SNCF_TRAIN_141R_ADDITIONAL_TEXT);
        articulated_part:             sw_141R_articulated_part;
        visual_effect_and_powered:    return (position_in_articulated_veh == 0) ?
                                          visual_effect_and_powered(VISUAL_EFFECT_STEAM, -5, DISABLE_WAGON_POWER) :
                                          visual_effect_and_powered(VISUAL_EFFECT_DISABLE, 0, DISABLE_WAGON_POWER);
        length:                       return (position_in_articulated_veh == 0) ? 7 : 4;
    }
}
